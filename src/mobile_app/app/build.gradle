plugins {
    id 'com.android.application'
    id 'kotlin-android'
    //protobuf-grpc
    id 'com.google.protobuf'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.roboticsmind.exammonitoringsystem"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
    // configuration for protobuf-lite
    sourceSets {
        main {
            // for IDE to find them
            java {
                srcDirs 'build/generated/source/proto/debug/java'
                srcDirs 'build/generated/source/proto/debug/grpc'
            }

            proto {
                // srcDir 'src/main/proto_lite'
                srcDir '../../rpc/proto_lite'
            }
        }
    }
}

// Protobuf gRPC
def grpcVersion = "1.42.1"
def protocVersion = "3.14.0"

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // CameraX
    def camerax_version = "1.1.0-alpha09"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"

    // CameraX View Class
    implementation "androidx.camera:camera-view:1.0.0-alpha29"

    // Tensorflow lite dependencies
    implementation 'org.tensorflow:tensorflow-lite:0.0.0-nightly'
    implementation 'org.tensorflow:tensorflow-lite-gpu:0.0.0-nightly'
    implementation 'org.tensorflow:tensorflow-lite-support:0.0.0-nightly'

    // Protobuf gRPC
    api("com.google.protobuf:protobuf-javalite:$protocVersion") { // lite for protobuf
        exclude module: "protobuf-lite"
    }
    //  api "com.squareup.wire:wire-runtime:3.5.0" // wire for protobuf
    api "io.grpc:grpc-protobuf-lite:$grpcVersion"
    api "io.grpc:grpc-core:$grpcVersion"
    api "io.grpc:grpc-stub:$grpcVersion"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    androidTestImplementation "io.grpc:grpc-okhttp:${grpcVersion}" // for client
    androidTestImplementation "io.grpc:grpc-netty-shaded:${grpcVersion}" // for server

    implementation 'io.grpc:grpc-netty:1.42.1'
    implementation 'io.grpc:grpc-netty-shaded:1.42.1'
    implementation 'io.grpc:grpc-stub:1.42.1'

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }

            task.plugins {
                grpc {
                    option 'lite'
                }
            }
        }
    }
}

android {
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }
}